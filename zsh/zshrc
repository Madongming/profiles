# Matthew Wang's zsh profile for general Linux/Unix
#
# Implementation note: all functions with name starts with '__' are considered
# private and will be unset at the end.

function __main
{
    local fn

    for fn in \
        __setup_path \
        __setup_shell_options \
        __setup_environments \
        __setup_aliases \
        __setup_completions \
        __setup_dir_colors \
        __setup_ssh_agent \
        __setup_prompt \
    ; do
        $fn; unset -f $fn
    done
}

function __prepend_path
{
    [[ :$PATH: == *:${1:?}:* ]] || PATH=$1:$PATH
}

function __setup_path
{
    local x

    # prepend paths
    for x in /sbin /usr/sbin /bin /usr/bin /usr/local/bin; do
        __prepend_path $x
    done

    # Load rbenv if installed, otherwise load ChefDK if installed
    if whence -p rbenv > /dev/null; then
        eval "$(rbenv init -)"
    elif [[ -x /usr/bin/chef ]]; then
        eval "$(/usr/bin/chef shell-init sh)"
    fi

    # ~/bin takes precedence
    __prepend_path ~/bin

    export PATH
}

function __setup_shell_options
{
    setopt prompt_subst
    setopt interactive_comments
    setopt nocase_glob
    setopt nocase_match 2>/dev/null     # does not work for zsh < 4.3
    setopt extended_history
    setopt hist_expire_dups_first
    setopt hist_ignore_dups
    setopt hist_ignore_space
    setopt hist_find_no_dups
    setopt hist_verify
    setopt share_history
    setopt auto_pushd
    setopt pushd_ignore_dups
    unsetopt nomatch
    unsetopt correct
    bindkey -e                          # Reclaim C-a, C-e, C-r, M-., etc.
    ! test -t 0 || stty stop undef      # Make 'C-s' to do fwd-i-search
    bindkey "^U" backward-kill-line     # Keep the same behavior as in bash
}

function __setup_environments
{
    export HISTFILE=~/.zhistory         # Prevent from ~/.zsh<tab> completion
    export HISTSIZE=10000
    export SAVEHIST=10000
    export EDITOR=vim

    # Locale (LC_*) matters for ls and sort on Linux, see also
    # www.gnu.org/software/coreutils/faq/#Sort-does-not-sort-in-normal-order_0021
    #
    [[ $(uname -s) != Linux ]] || export LC_COLLATE=C
}

function __setup_completions
{
    [[ ! -d ~/.zsh-completions ]] || fpath=(~/.zsh-completions/src $fpath)
    autoload -U compinit && compinit
    zstyle ':completion:*' menu yes select
    zstyle ':completion:*' users off
    zmodload zsh/complist
    bindkey -M menuselect '^M' .accept-line     # <Enter> only once to accept
    ZLE_REMOVE_SUFFIX_CHARS=                    # no space after, see zshparam(1)

    # Fix default host completion
    __hosts=($(sed -ne 's/[, ].*//p' ~/.ssh/known_hosts* 2>/dev/null))
    zstyle ':completion:*:hosts' hosts $__hosts
}

function __setup_aliases
{
    alias ..='cd ..'
    alias ...='cd ../..'
    alias ....='cd ../../..'
    alias .....='cd ../../../..'
    alias pd='popd'
    # Skip system wide vimrc to reduce startup time
    ! type vim >& /dev/null || alias vi='vim -Xn -u ~/.vimrc'
    alias grep='grep --color=auto'

    case $(uname -s) in
        Linux)
            alias ls='/bin/ls -F --color=auto'
            alias l='/bin/ls -lF --color=auto'
            alias lsps='ps -ef f | grep -vw grep | grep -i'
            ;;
        Darwin)
            alias ls='/bin/ls -F'
            alias l='/bin/ls -lF'
            alias lsps='ps -ax -o user,pid,ppid,stime,tty,time,command | grep -vw grep | grep -i'
            ;;
        *)
            alias ls='/bin/ls -F'
            alias l='/bin/ls -lF'
            alias lsps='ps -auf | grep -vw grep | grep -i'
            ;;
    esac
}

function __setup_dir_colors
{
    if type dircolors >& /dev/null && [[ -f ~/.dircolors ]]; then
        eval $(dircolors -b ~/.dircolors)
    fi
}

function __has_ssh_key
{
    [[ -f ~/.ssh/$USER.key ]] || ls ~/.ssh/id_?sa >& /dev/null
}

function __load_ssh_key
{
    [[ ! -f "${1:?}" ]] || ssh-add -L | grep -qw "$1" || ssh-add "$1"
}

# ssh-add -l exits code 2 when unable to connect to the agent
function __setup_ssh_agent
{
    local rc=~/.ssh-agent.rc

    __has_ssh_key || return 0
    [[ ! -f $rc ]] || source $rc
    if [[ $(ssh-add -l >& /dev/null; echo $?) == 2 ]]; then
        print -P "%{\e[31m%}Starting a new ssh-agent process...%{\e[0m%}" >&2
        rm -f ~/.ssh-agent.sock
        ssh-agent -s -a ~/.ssh-agent.sock | sed '/^echo/d' > $rc
        source $rc
    fi

    __load_ssh_key ~/.ssh/$USER.key
    __load_ssh_key ~/.ssh/id_rsa
    __load_ssh_key ~/.ssh/id_dsa
}

# Non "private" helper function used to setup PROMPT
function _git_active_branch
{
    local branch info age track

    [[ $(git rev-parse --is-inside-work-tree 2>/dev/null) == true ]] || return
    branch=$(git symbolic-ref HEAD 2>/dev/null)
    branch=${branch#refs/heads/}
    info=$(git status -s 2>/dev/null)
    age=$(git log --pretty=format:'%cr' -1 refs/heads/$branch 2>/dev/null)
    track=$(git status -sb 2>/dev/null | sed -n 's/^##.*\[\(.*\)\].*/, \1/p')

    # XXX: $_DR and $_DG won't expand here
    if [[ -z $info ]]; then
        print -nP "%{\e[32m%} ($branch) %{\e[36m%}[${age}${track}]"
    elif [[ -z $(echo "$info" | grep -v '^??') ]]; then
        print -nP "%{\e[35m%} ($branch) %{\e[36m%}[${age}${track}]"
    else
        print -nP "%{\e[31m%} ($branch) %{\e[36m%}[${age}${track}]"
    fi
}

# Fancy PROMPT, prompt exit status of last command, currenet time, hostname,
# time, cwd, git status and branch, also prompt the '%' in reverse color when
# we have background jobs.
#
function __setup_prompt
{
    local _DR=$'%{\e[31m%}'       # red
    local _DG=$'%{\e[32m%}'       # green
    local _DY=$'%{\e[33m%}'       # yellow
    local _DB=$'%{\e[34m%}'       # blue
    local _DM=$'%{\e[35m%}'       # magenta
    local _DC=$'%{\e[36m%}'       # cyan
    local _RV=$'%{\e[7m%}'        # reverse
    local _NC=$'%{\e[0m%}'        # reset color

    PROMPT="\$([[ \$? == 0 ]] && echo '${_DG}✔' || echo '${_DR}✘') %* "

    # Detect whether this box has ssh keys, distinguish hostname color and setup
    # ssh-agent related environment accordingly
    #
    if __has_ssh_key; then
        # I am on my own machine, try load ssh-agent related environments
        PROMPT+="${_DB}"                                # blue hostname
    else
        # Otherwise assume I am on other's box, highlight hostname in magenta
        PROMPT+="${_DM}"                                # magenta hostname
    fi

    # Highlight hostname in reverse green if inside a container
    if [[ -n $container_uuid ]] || [[ -f /.dockerenv ]]; then
        PROMPT+="${_RV}${_DG}"
    fi
    PROMPT+="$(hostname -f)"
    PROMPT+="${_NC}:${_DY}%~${_NC}"                     # yellow cwd
    PROMPT+='$(_git_active_branch)'                     # colorful git branch name
    PROMPT+=" ${_DC}"$'⤾\n'                             # cyan wrap char, newline
    PROMPT+="\$([[ -z \$(jobs) ]] || echo '${_RV}')"    # reverse bg job indicator
    PROMPT+="%#${_NC} "                                 # % or #
}

# Find a file which name matches given pattern (ERE, case insensitive)
function f
{
    local pat=${1?'Usage: f ERE-pattern [path...]'}
    shift
    find ${@:-.} \( -path '*/.svn' -o -path '*/.git' -o -path '*/.idea' \) \
        -prune -o -print -follow | grep -iE "$pat"
}

# Load file list generated by f() in vim, type 'gf' to jump to the file
function vif
{
    local tmpf=/tmp/viftmpfile.$RANDOM$$
    f "$@" > $tmpf && vi -c "/$1" $tmpf && rm -f $tmpf
}

# Grep a ERE pattern in cwd or given path
function g
{
    local string_pat=${1:?"Usage: g ERE-pattern [grep opts] [path...]"}
    shift
    local grep_opts="--color=auto"
    local paths

    while (( $# > 0 )); do
        case "$1" in
            -*) grep_opts="$grep_opts $1"; shift;;
            *) paths="$paths $1"; shift;;
        esac
    done
    paths=${paths# }
    grep_opts=${grep_opts# }
    [[ -n "$paths" ]] || paths="."

    find $paths \( -path '*/.svn' -o -path '*/.git' -o -path '*/.idea' \) \
        -prune -o -type f -print0 -follow \
        | eval "xargs -0 -P128 grep -EH $grep_opts '$string_pat'"
}

########################################################################
# Setup everything and unset "private" functions
########################################################################

__main
unset -f __prepend_path __has_ssh_key __load_ssh_key __main

# vim:set et sts=4 sw=4 ft=zsh:
